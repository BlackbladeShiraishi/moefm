buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:2.2.2'
    classpath 'me.tatarka:gradle-retrolambda:3.3.0'
    classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.1'

    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
// uncomment this to show dexcount. not use always for faster build
//apply plugin: 'com.getkeepsafe.dexcount'

android {
  compileSdkVersion 25
  buildToolsVersion "25.0.0"

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  defaultConfig {
    applicationId "com.github.blackbladeshiraishi.fm.moe.client.android"
    minSdkVersion 15
    targetSdkVersion 25
    versionCode version.versionCode
    versionName version.versionName
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  packagingOptions {
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/groovy-release-info.properties'
  }
  lintOptions {
    // TODO remove this when https://issues.apache.org/jira/browse/GROOVY-7296 resolved
    // groovy runtime library have some references to invalid package on Android
    warning 'InvalidPackage'
  }
}

// set string android res value in settings.properties file
new ConfigSlurper().parse(file('settings.properties').getText('UTF-8'))
    .android.resValue.string.each {key, value ->
      project.android.applicationVariants.all {variant ->
        variant.resValue "string", key, value
      }
    }

dependencies {
  def androidSupportVersion = '25.0.0'

  compile "org.codehaus.groovy:groovy:${groovyVersion}:grooid"
  compile(project(':facade')) {
    // use the 'grooid' classifier of groovy library as above
    exclude group: 'org.codehaus.groovy', module: 'groovy'
  }
  compile(project(':business:impl-core')) {
    // use the 'grooid' classifier of groovy library as above
    exclude group: 'org.codehaus.groovy', module: 'groovy'
  }
  compile(project(':business:impl-moefm')) {
    // use the 'grooid' classifier of groovy library as above
    exclude group: 'org.codehaus.groovy', module: 'groovy'
  }

  compile "com.android.support:appcompat-v7:$androidSupportVersion"
  compile "com.android.support:design:$androidSupportVersion"
  compile "com.android.support:recyclerview-v7:$androidSupportVersion"
  compile "com.android.support:cardview-v7:$androidSupportVersion"
  compile 'com.squareup.flow:flow:1.0.0-alpha2'
  compile 'me.zhanghai.android.materialprogressbar:library:1.3.0'
  compile 'com.squareup.picasso:picasso:2.5.2'

  compile 'io.reactivex:rxandroid:1.2.1'
  // Because RxAndroid releases are few and far between, it is recommended you also
  // explicitly depend on RxJava's latest version for bug fixes and new features.
  compile "io.reactivex:rxjava:$rxjavaVersion"
  /* bug see https://github.com/ReactiveX/RxGroovy/issues/7
  compile ('io.reactivex:rxgroovy:1.0.3') {
      // use the 'grooid' classifier of groovy library as above
      exclude group: 'org.codehaus.groovy', module: 'groovy-all'
  }*/
  compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
  compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.4.0'

  //Dagger 2
  compile "com.google.dagger:dagger:$daggerVersion"
  annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

  testCompile "junit:junit:$junitVersion"
}
