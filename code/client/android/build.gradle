buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:2.0.0-beta7'
    classpath 'org.codehaus.groovy:gradle-groovy-android-plugin:0.3.10'
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.3.0'

    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

apply plugin: 'com.android.application'
apply plugin: 'groovyx.grooid.groovy-android'
apply plugin: 'com.neenbedankt.android-apt'
// uncomment this to show dexcount. not use always for faster build
//apply plugin: 'com.getkeepsafe.dexcount'

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.3"

  defaultConfig {
    applicationId "com.github.blackbladeshiraishi.fm.moe.client.android"
    minSdkVersion 15
    targetSdkVersion 23
    versionCode version.versionCode
    versionName version.versionName
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  packagingOptions {
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/groovy-release-info.properties'
  }
  lintOptions {
    // TODO remove this when https://issues.apache.org/jira/browse/GROOVY-7296 resolved
    // groovy runtime library have some references to invalid package on Android
    warning 'InvalidPackage'
  }
}

// set string android res value in local.properties file
new ConfigSlurper().parse(file('local.properties').getText('UTF-8'))
    .android.resValue.string.each {key, value ->
      project.android.applicationVariants.all {variant ->
        variant.resValue "string", key, value
      }
    }

androidGroovy {
  options {
    configure(groovyOptions) {
      encoding = 'UTF-8'
      configurationScript = file "$rootDir/config/groovy/compiler-config.groovy"
    }
  }
}

dependencies {
  def androidSupportVersion = '23.2.1'
  def daggerVersion = '2.1'

  compile "org.codehaus.groovy:groovy:${groovyVersion}:grooid"
  compile(project(':facade')) {
    // use the 'grooid' classifier of groovy library as above
    exclude group: 'org.codehaus.groovy', module: 'groovy'
  }

  compile "com.android.support:appcompat-v7:$androidSupportVersion"
  compile "com.android.support:design:$androidSupportVersion"
  compile "com.android.support:recyclerview-v7:$androidSupportVersion"
  compile 'me.zhanghai.android.materialprogressbar:library:1.1.5'

  compile 'io.reactivex:rxandroid:1.1.0'
  // Because RxAndroid releases are few and far between, it is recommended you also
  // explicitly depend on RxJava's latest version for bug fixes and new features.
  compile "io.reactivex:rxjava:$rxjavaVersion"
  /* bug see https://github.com/ReactiveX/RxGroovy/issues/7
  compile ('io.reactivex:rxgroovy:1.0.3') {
      // use the 'grooid' classifier of groovy library as above
      exclude group: 'org.codehaus.groovy', module: 'groovy-all'
  }*/

  //Dagger 2
  compile "com.google.dagger:dagger:$daggerVersion"
  apt "com.google.dagger:dagger-compiler:$daggerVersion"

  testCompile "junit:junit:$junitVersion"
}
