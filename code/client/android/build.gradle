buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:1.3.1'
    classpath 'org.codehaus.groovy:gradle-groovy-android-plugin:0.3.8'

    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

apply plugin: 'com.android.application'
apply plugin: 'groovyx.grooid.groovy-android'

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.2"

  defaultConfig {
    applicationId "com.github.blackbladeshiraishi.fm.moe.client.android"
    minSdkVersion 15
    targetSdkVersion 23
    versionCode version.versionCode
    versionName version.versionName
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  packagingOptions {
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
  }
  lintOptions {
    // TODO remove this when https://issues.apache.org/jira/browse/GROOVY-7296 resolved
    // groovy runtime library have some references to invalid package on Android
    warning 'InvalidPackage'
  }
}

// set string android res value in local.properties file
new ConfigSlurper().parse(file('local.properties').getText('UTF-8'))
    .android.resValue.string.each {key, value ->
      project.android.applicationVariants.all {variant ->
        variant.resValue "string", key, value
      }
    }

androidGroovy {
  options {
    configure(groovyOptions) {
      encoding = 'UTF-8'
      configurationScript = file "$rootDir/config/groovy/compiler-config.groovy"
    }
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
  }
}

dependencies {
  compile 'org.codehaus.groovy:groovy:2.4.6:grooid'
  compile(project(':facade')) {
    // use the 'grooid' classifier of groovy library as above
    exclude group: 'org.codehaus.groovy', module: 'groovy'
  }

  compile 'com.android.support:appcompat-v7:23.2.0'
  compile 'com.android.support:recyclerview-v7:23.2.0'
  compile 'io.reactivex:rxandroid:1.1.0'
  // Because RxAndroid releases are few and far between, it is recommended you also
  // explicitly depend on RxJava's latest version for bug fixes and new features.
  compile 'io.reactivex:rxjava:1.1.0'
  /* bug see https://github.com/ReactiveX/RxGroovy/issues/7
  compile ('io.reactivex:rxgroovy:1.0.3') {
      // use the 'grooid' classifier of groovy library as above
      exclude group: 'org.codehaus.groovy', module: 'groovy-all'
  }*/

  testCompile 'junit:junit:4.12'
}

// for VectorDrawable support library when use Android Plugin for Gradle *1.5.0 or lower*
// see http://developer.android.com/tools/support-library/index.html
android {
  defaultConfig {
    // Stops the Gradleâ€™s automatic rasterization of vectors
    //generatedDensities = [] // 1.3.1 doesn't has this feature
  }
  // Flag that tells aapt to keep the attribute ids
  aaptOptions {
    additionalParameters "--no-version-vectors"
  }
}
